apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "com.exsilicium.wearesv"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 5
        versionName "0.1.0"
        signingConfig signingConfigs.debug
    }
    buildTypes {
        debug {
            versionNameSuffix "-alpha"
        }
        release {
            versionNameSuffix "-beta"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    viewBinding {
        enabled = true
    }

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def apiKey = "\"Your API Key here\""
    try {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
        apiKey = keystoreProperties['esvApiKey']
    } catch (FileNotFoundException ignored) {
    }
    buildTypes.each {
        it.buildConfigField 'String', 'ESV_API_KEY', apiKey
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'

    implementation 'com.google.android.gms:play-services-wearable:17.1.0'
    implementation 'com.google.android.support:wearable:2.8.1'

    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation 'androidx.wear:wear:1.1.0'

    implementation 'com.exsilicium.scripture:scripture-core:0.1.2'
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"

    compileOnly 'com.google.android.wearable:wearable:2.8.1'

    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
}
